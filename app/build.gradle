//noinspection GradleCompatible
apply plugin: 'com.android.application'

android {
    packagingOptions {
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    compileSdkVersion 'android-P'

    defaultConfig {
        applicationId "biz.dealnote.messenger"
        minSdkVersion 'P'
        targetSdkVersion 'P'
        versionCode 40999
        versionName '4_0_99'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    lintOptions {
        warning 'InvalidPackage'
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled true
            multiDexEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            multiDexEnabled false
        }
    }

    /*applicationVariants.all { variant ->
        def flavor = variant.mergedFlavor
        def versionCode = flavor.versionCode

        def versionName = flavor.versionName
        if (variant.buildType.isDebuggable()) {
            versionCode += 1
            versionName = versionCode + "-d"
        }

        flavor.versionCode = versionCode
        flavor.versionName = versionName
    }*/

    flavorDimensions "type"

    productFlavors {
        full {
            applicationId "ru.ezorrio.phoenix"
            dimension "type"
            buildConfigField 'int', 'VK_API_APP_ID', getProps("full_vk_app_id")
            buildConfigField 'String', 'GCM_SENDER_ID', getProps("full_gcm_sender_id")
            buildConfigField 'boolean', 'FULL_APP', 'true'
            buildConfigField 'String', 'SERVICE_TOKEN', getProps("full_vk_service_token")
            buildConfigField 'String', 'VK_CLIENT_SECRET', getProps("full_vk_client_secret")
            buildConfigField 'String', 'YOUTUBE_DEV_KEY', getProps("full_youtube_dev_key")
        }
        lite {
            applicationId "biz.dealnote.messenger"
            dimension "type"
            buildConfigField 'int', 'VK_API_APP_ID', getProps("lite_vk_app_id")
            buildConfigField 'String', 'GCM_SENDER_ID', getProps("lite_gcm_sender_id")
            buildConfigField 'boolean', 'FULL_APP', 'false'
            buildConfigField 'String', 'SERVICE_TOKEN', getProps("lite_vk_service_token")
            buildConfigField 'String', 'VK_CLIENT_SECRET', getProps("lite_vk_client_secret")
            buildConfigField 'String', 'YOUTUBE_DEV_KEY', getProps("lite_youtube_dev_key")
        }
    }
}

def getProps(String propName) {
    def propsFile = rootProject.file('build-config-fields.properties')

    if (propsFile.exists()) {
        def props = new Properties()

        FileInputStream fis
        try {
            fis = new FileInputStream(propsFile)
            props.load(fis)

            def value = props[propName]
            fis.close()
            return value
        } finally {
            if(fis != null) fis.close()
        }
    } else {
        throw new GradleException('File build-config-fields.properties not found')
    }
}

allprojects {
    repositories {
        maven {
            url "https://jitpack.io"
        }

        maven {
            url 'https://maven.google.com'
        }
    }
}

ext {
    androidXLibraryVersion = '1.0.0-alpha1'
    retrofitLibraryVersion = '2.4.0'
    okhttpLibraryVersion = '3.10.0'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.google.android.material:material:$androidXLibraryVersion"
    implementation "androidx.appcompat:appcompat:$androidXLibraryVersion"
    implementation "androidx.recyclerview:recyclerview:$androidXLibraryVersion"
    implementation "androidx.cardview:cardview:$androidXLibraryVersion"
    implementation "androidx.viewpager:viewpager:$androidXLibraryVersion"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$androidXLibraryVersion"
    implementation "androidx.drawerlayout:drawerlayout:$androidXLibraryVersion"
    implementation "androidx.vectordrawable:vectordrawable:$androidXLibraryVersion"
    implementation "androidx.media:media:$androidXLibraryVersion"
    implementation "androidx.browser:browser:$androidXLibraryVersion"
    implementation "androidx.exifinterface:exifinterface:$androidXLibraryVersion"
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'com.github.chrisbanes:PhotoView:2.1.3'
    implementation 'com.google.code.gson:gson:2.8.4'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.13'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation files('libs/YouTubeAndroidPlayerApi.jar')
    implementation project(path: ':mvpcore', configuration: 'default')
    implementation "com.squareup.retrofit2:retrofit:$retrofitLibraryVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitLibraryVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitLibraryVersion"
    implementation "com.squareup.okhttp3:okhttp:$okhttpLibraryVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpLibraryVersion"
    implementation "com.google.firebase:firebase-core:16.0.0"
    implementation "com.google.firebase:firebase-crash:16.0.0"
    implementation "com.google.firebase:firebase-database:16.0.1"
    implementation "com.google.firebase:firebase-messaging:17.0.0"
    implementation 'com.google.android.exoplayer:exoplayer-core:2.8.0'
}

apply plugin: 'com.google.gms.google-services'